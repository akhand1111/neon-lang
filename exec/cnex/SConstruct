import sys

Import(["env"])

envcnex = env.Clone()

if sys.platform == "win32":
    envcnex.Append(CFLAGS=[
        "/W4",
        "/WX",
        "/MDd",
        "/wd4996", # CRT deprecation warnings
    ])
else:
    envcnex.Append(CFLAGS=[
        "-std=c99",
        "-Wall",
        "-Werror",
    ])
    if "RELEASE" in envcnex and not envcnex["RELEASE"]:
        envcnex.Append(CFLAGS=[
            "-g",
        ])
    envcnex.Append(LIBS="m")

envcnex.Append(CPPPATH=[Dir(".")])

test_number_to_string_c = envcnex.Program("test_number_to_string_c", [
    "test_number_to_string.c",
    "number.c",
])

test_path_support = envcnex.Program("test_path_support", [
    "test_path_support.c",
    "support.c",
    "util.c",
])

rtl_c = ([
    "lib/binary.c",
    "lib/io.c",
    "lib/math.c",
    "lib/random.c",
    "lib/runtime.c",
    "lib/sys.c",
    "lib/textio.c",
])

cnex = envcnex.Program("cnex", [
    "array.c",
    "bytecode.c",
    "cell.c",
    "cnex.c",
    "dictionary.c",
    "framestack.c",
    "global.c",
    "module.c",
    "nstring.c",
    "number.c",
    "object.c",
    "stack.c",
    "support.c",
    "util.c",
    rtl_c,
],
)

envcnex.Command("tests_number", test_number_to_string_c, test_number_to_string_c[0].path)
envcnex.Command("tests_paths", test_path_support, test_path_support[0].path)

Return(["cnex"])
